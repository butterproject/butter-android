import si.kamino.gradle.extensions.version.code.IncreaseVersionCode

buildscript {
    ext {
        kotlinVersion = '1.2.41'
    }

    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
        classpath "gradle.plugin.si.kamino.gradle:android-version:1.5.3"
        classpath 'com.jakewharton:butterknife-gradle-plugin:8.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath("solar.blaz.gradle:play-publisher:0.3.0-SNAPSHOT")
    }
}

apply plugin: "solar.blaz.play-publisher-parent"

ext {
    appCompatVersion = "27.1.1"
    targetSdk = 27
    compileSdk = 27
    minSdk = 16
    butterknifeVersion = "8.8.1"
    okHttpVersion = "3.4.2"
    daggerVersion = "2.15"
    javaxAnnotationVersion = "1.2"
    torrentstreamVersion = '2.5.0'
    filePickerVersion = '1.1.0'
    changeloglibVersion = '2.1.0'
    gsonVersion = '2.8.1'
    rxJavaVersion = '2.1.8'
    retrofitVersion = '2.4.0'
    glideVersion = '4.6.1'
}

allprojects {
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'http://guardian.github.com/maven/repo-releases' }
        maven { url "https://dl.bintray.com/butterproject/maven/" }
    }

    //let project evaluate its script first
    project.afterEvaluate {
        //only apply to projects that have the android plugin
        if (project.plugins.hasPlugin('com.android.application')) {

            project.androidVersion {
                appVersion {
                    versionCode {
                        digits 2
                    }
                }

                splits {
                    abi {
                        "armeabi-v7a" {
                            versionCode(IncreaseVersionCode) {
                                add 2000000
                            }
                        }

                        "arm64-v8a" {
                            versionCode(IncreaseVersionCode) {
                                add 3000000
                            }
                        }

                        x86 {
                            versionCode(IncreaseVersionCode) {
                                add 4000000
                            }
                        }

                        x86_64 {
                            versionCode(IncreaseVersionCode) {
                                add 5000000
                            }
                        }
                    }
                }
            }
        }
    }
}

playPublisher {
    artifacts {
        draftInternal {
            appId = "org.butterproject.butter"
            clientSecretJson = project.hasProperty("playPublishFile") ? file(playPublishFile) : null
            action = "draft"
            track = "internal"
        	listingDir = file(".butter/play")
        }

        draftProduction {
            appId = "org.butterproject.butter"
            clientSecretJson = project.hasProperty("playPublishFile") ? file(playPublishFile) : null
            action = "draft"
            track = "production"
			listingDir = file(".butter/play")        }
    }
}

