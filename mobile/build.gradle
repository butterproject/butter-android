apply plugin: 'com.android.application'
apply plugin: 'hugo'//for method logging (only in debug builds)
apply plugin: 'com.github.ben-manes.versions'// to check for dependency updates

android {
    compileSdkVersion rootProject.ext.compileSdk
    def buildNumber = (System.getenv("BUILD_NUMBER") ?: 1) as int

    defaultConfig {
        applicationId "pct.droid"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode buildNumber
        versionName "0.2.11"
        multiDexEnabled true
    }

    signingConfigs {
        mainConfig {
            keyAlias System.getenv("S_ALIAS").toString() ?: ""
            keyPassword System.getenv("S_KEYPASS").toString() ?: ""
            storeFile System.getenv("S_KEYSTORE").toString() ? file(System.getenv("S_KEYSTORE").toString()) : file("")
            storePassword System.getenv("S_KEYPASS").toString() ?: ""
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.mainConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".dev"
        }
    }

    flavorDimensions 'platform'
    productFlavors {
        arm {
            dimension 'platform'
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
        x86 {
            dimension 'platform'
            ndk {
                abiFilters "x86", "x86_64"
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
    }

    project.ext.versionCodes = ["arm": 3, "x86": 5]
}

//to check for dependency updates,
//run ./gradlew dependencyUpdates -Drevision=release
dependencies {
    implementation project(':base')

    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.mediarouter:mediarouter:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'

    implementation 'de.hdodenhof:circleimageview:2.0.0'
    implementation 'com.github.nirhart:parallaxscroll:dd53d1f9d1'
    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'com.github.guardian:Option:-SNAPSHOT' 
    implementation 'net.rdrei.android.dirchooser:library:3.0@aar'

    implementation 'com.sothree.slidinguppanel:library:3.2.0'

    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${rootProject.ext.butterknifeVersion}"

    provided "javax.annotation:javax.annotation-api:${rootProject.ext.javaxAnnotationVersion}"
    annotationProcessor "javax.annotation:javax.annotation-api:${rootProject.ext.javaxAnnotationVersion}"

}
