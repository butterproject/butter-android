import si.kamino.gradle.extensions.version.FileAppVersion
import si.kamino.gradle.extensions.version.StaticVersion
import si.kamino.gradle.extensions.version.code.SimpleVersionCode

/*
 * This file is part of Butter.
 *
 * Butter is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Butter is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Butter. If not, see <http://www.gnu.org/licenses/>.
 */
apply from: "${project.rootDir}/config/gradle/android-application-config.gradle"

android {
    defaultConfig {
        applicationId "org.butterproject.butter"
        minSdkVersion rootProject.ext.minSdkTV
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".dev"
        }
    }

    lintOptions {
        abortOnError false
    }

    splits {
        abi {
            enable true
            universalApk false
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.0.0-rc01'
    implementation 'androidx.palette:palette:1.0.0-rc01'
    implementation 'androidx.leanback:leanback:1.0.0-rc01'

    implementation project(':base')

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
}

class SimpleIncreaseVersionCode extends SimpleVersionCode {

    @Override
    int buildVersionCode(final StaticVersion version) {
        return super.buildVersionCode(version) + 10000000
    }

}

androidVersion {

    appVersion(FileAppVersion) {
        sourceFile rootProject.file(".butter/tv.properties")
        versionCode(SimpleIncreaseVersionCode) {
            digits 2
        }
    }

}
