apply plugin: 'com.android.application'
apply plugin: 'hugo'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion rootProject.ext.compileSdk
    def buildNumber = (System.getenv("BUILD_NUMBER") ?: 1) as int

    defaultConfig {
        applicationId "pct.droid.tv"
        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdk
        versionCode buildNumber
        versionName "0.1.5"
        multiDexEnabled true
    }

    signingConfigs {
        mainConfig {
            keyAlias System.getenv("S_ALIAS").toString() ?: ""
            keyPassword System.getenv("S_KEYPASS").toString() ?: ""
            storeFile System.getenv("S_KEYSTORE").toString() ? file(System.getenv("S_KEYSTORE").toString()) : file("")
            storePassword System.getenv("S_KEYPASS").toString() ?: ""
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.mainConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix ("#" + (buildNumber == 0 ? "local" : buildNumber).toString())
            applicationIdSuffix ".dev"
        }
    }

    flavorDimensions 'platform'
    productFlavors {
        arm {
            dimension 'platform'
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
        x86 {
            dimension 'platform'
            ndk {
                abiFilters "x86", "x86_64"
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    project.ext.versionCodes = ["arm": 3, "x86": 5]
}

//./gradlew dependencyUpdates -Drevision=release
dependencies {
    implementation project(':base')

    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.leanback:leanback:1.0.0'
    implementation "com.squareup.picasso:picasso:${rootProject.ext.picassoVersion}"
    implementation "com.jakewharton:butterknife:${rootProject.ext.butterknifeVersion}"
    implementation 'de.greenrobot:eventbus:2.4.1'

    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${rootProject.ext.butterknifeVersion}"
    compileOnly "javax.annotation:javax.annotation-api:${rootProject.ext.javaxAnnotationVersion}"
    annotationProcessor "javax.annotation:javax.annotation-api:${rootProject.ext.javaxAnnotationVersion}"
}
