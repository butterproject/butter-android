apply plugin: 'com.android.library'
apply plugin: 'hugo'//for method logging (only in debug builds)
apply plugin: 'com.jakewharton.butterknife'
apply from: '../config/gradle/checkstyle.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildToolsVersion

    def buildNumber = (System.getenv('BUILD_NUMBER') == null ? 0 : System.getenv('BUILD_NUMBER')) as int
    def gitBranch = (System.getenv('GIT_BRANCH') == null ? 'local' : System.getenv('GIT_BRANCH').replace('origin/', ''))
    def releaseType = (System.getenv('RELEASE_TYPE') == null ? 'NONE' : System.getenv('RELEASE_TYPE'))
    def updateUrls = (System.getenv('UPDATE_URLS') == null ? '"http://localhost/android-update.json"' : System.getenv('UPDATE_URLS'))

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        buildConfigField 'Integer', 'BUILD_NUMBER', '' + buildNumber + ''
        buildConfigField 'String', 'GIT_BRANCH', '\"' + gitBranch + '\"'
        buildConfigField 'String', 'RELEASE_TYPE', '\"' + releaseType + '\"'
        buildConfigField 'String[]', 'UPDATE_URLS', '{' + updateUrls + '}'
        versionCode buildNumber
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:multidex:1.0.1'
    api "com.android.support:support-v4:$appCompatVersion"
    api "com.android.support:palette-v7:$appCompatVersion"
    api "com.android.support:appcompat-v7:$appCompatVersion"
    api "com.android.support:design:$appCompatVersion"

    debugImplementation 'com.github.simonpercic:oklog3:2.1.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5'

    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    api 'com.jakewharton.timber:timber:4.5.1'
    api "com.squareup.okhttp3:okhttp:$okHttpVersion"
    api 'com.squareup.picasso:picasso:2.5.2'
    api 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    api "com.google.code.gson:gson:$gsonVersion"
    api "com.jakewharton:butterknife:$butterknifeVersion"
    api "com.github.gabrielemariotti.changeloglib:changelog:${changeloglibVersion}"
    api 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    api 'com.koushikdutta.async:androidasync:2.1.9'

    api "com.github.se-bastiaan:TorrentStream-Android:${torrentstreamVersion}"

    implementation 'de.timroes:aXMLRPC:1.8.1'
    api "com.google.dagger:dagger:$daggerVersion"

    api 'com.butterproject:vlc-android-sdk:2.0.6'
    api project(':connectsdk')
    api project(':provider:provider')
    api project(':provider:mock')
    api project(':provider:vodo')

    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
}
