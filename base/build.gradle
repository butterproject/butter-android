apply plugin: 'com.android.library'
apply plugin: 'hugo'//for method logging (only in debug builds)
apply plugin: 'com.github.ben-manes.versions'// to check for dependency updates

android {
    compileSdkVersion rootProject.ext.compileSdk

    def buildNumber = (System.getenv('BUILD_NUMBER') == null ? 0 : System.getenv('BUILD_NUMBER')) as int
    def gitBranch = (System.getenv('GIT_BRANCH') == null ? 'local' : System.getenv('GIT_BRANCH').replace('origin/', ''))
    def releaseType = (System.getenv('RELEASE_TYPE') == null ? 'NONE' : System.getenv('RELEASE_TYPE'))
    def updateUrls = (System.getenv('UPDATE_URLS') == null ? '"https://popcorntime.sh/android-update.json"' : System.getenv('UPDATE_URLS'))
    def animeUrls = (System.getenv('ANIME_URLS') == null ? '"https://popcorn-time.ga/"' : System.getenv('ANIME_URLS'))
    def movieUrls = (System.getenv('MOVIE_URLS') == null ? '"https://popcorn-time.ga/"' : System.getenv('MOVIE_URLS'))
    def tvUrls = (System.getenv('TV_URLS') == null ? '"https://popcorn-time.ga/"' : System.getenv('TV_URLS'))

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        buildConfigField 'Integer', 'BUILD_NUMBER', '' + buildNumber + ''
        buildConfigField 'String', 'GIT_BRANCH', '\"' + gitBranch + '\"'
        buildConfigField 'String', 'RELEASE_TYPE', '\"' + releaseType + '\"'
        buildConfigField 'String[]', 'UPDATE_URLS', '{' + updateUrls + '}'
        buildConfigField 'String[]', 'ANIME_URLS', '{' + animeUrls + '}'
        buildConfigField 'String[]', 'MOVIE_URLS', '{' + movieUrls + '}'
        buildConfigField 'String[]', 'TV_URLS', '{' + tvUrls + '}'
        versionCode buildNumber
        multiDexEnabled true
        resValue 'string', 'sentry_dsn', rootProject.ext.sentryDsn
        resValue 'string', 'dht', rootProject.ext.dht
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    api project(':vlc')
    api project(':connectsdk')

    api 'androidx.multidex:multidex:2.0.1'

    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.palette:palette:1.0.0'
    api 'com.google.android.material:material:1.3.0'

    debugApi 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseApi 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    api 'com.jakewharton.timber:timber:4.1.2'
    api "com.squareup.okhttp3:okhttp:${rootProject.ext.okHttpVersion}"
    api "com.squareup.picasso:picasso:${rootProject.ext.picassoVersion}"
    api "com.jakewharton:butterknife:${rootProject.ext.butterknifeVersion}"

    api 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'

    api 'com.koushikdutta.async:androidasync:2.1.9'

    api 'de.timroes:aXMLRPC:1.8.1'

    api "com.google.dagger:dagger:${rootProject.ext.daggerVersion}"

    api 'com.fasterxml.jackson.core:jackson-core:2.7.2'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.7.2'
    api 'com.fasterxml.jackson.core:jackson-databind:2.7.2'

    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${rootProject.ext.butterknifeVersion}"

    implementation "javax.annotation:javax.annotation-api:${rootProject.ext.javaxAnnotationVersion}"
    annotationProcessor "javax.annotation:javax.annotation-api:${rootProject.ext.javaxAnnotationVersion}" 

	implementation 'io.sentry:sentry-android:4.3.0'

    api project(":StreamServerTorrent")
}
