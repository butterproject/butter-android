apply plugin: 'com.android.library'
apply plugin: 'hugo'//for method logging (only in debug builds)
apply plugin: 'com.github.ben-manes.versions'// to check for dependency updates

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildToolsVersion

    def buildNumber = (System.getenv('BUILD_NUMBER') == null ? 0 : System.getenv('BUILD_NUMBER')) as int
    def gitBranch = (System.getenv('GIT_BRANCH') == null ? 'local' : System.getenv('GIT_BRANCH').replace('origin/', ''))
    def releaseType = (System.getenv('RELEASE_TYPE') == null ? 'NONE' : System.getenv('RELEASE_TYPE'))
    def updateUrls = (System.getenv('UPDATE_URLS') == null ? '"http://localhost/android-update.json"' : System.getenv('UPDATE_URLS'))
    def movieUrls = (System.getenv('MOVIE_URLS') == null ? '"https://movies-v2.api-fetch.website/"' : System.getenv('MOVIE_URLS'))
    def tvUrls = (System.getenv('TV_URLS') == null ? '"http://api-fetch.website/tv/"' : System.getenv('TV_URLS'))

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        buildConfigField 'Integer', 'BUILD_NUMBER', '' + buildNumber + ''
        buildConfigField 'String', 'GIT_BRANCH', '\"' + gitBranch + '\"'
        buildConfigField 'String', 'RELEASE_TYPE', '\"' + releaseType + '\"'
        buildConfigField 'String[]', 'UPDATE_URLS', '{' + updateUrls + '}'
        buildConfigField 'String[]', 'MOVIE_URLS', '{' + movieUrls + '}'
        buildConfigField 'String[]', 'TV_URLS', '{' + tvUrls + '}'
        versionCode buildNumber
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
        exclude 'META-INF/DEPENDENCIES'
    }

    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:multidex:1.0.1'

    compile "com.android.support:support-v4:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:palette-v7:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:appcompat-v7:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:design:${rootProject.ext.appCompatVersion}"

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.6.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.uwetrottmann:trakt-java:4.5.2'
    compile 'com.github.gabrielemariotti.changeloglib:changelog:2.0.0'

    compile 'com.koushikdutta.async:androidasync:2.1.6'

    compile 'com.github.sv244:torrentstream-android:1.1.0'

    compile 'de.timroes:aXMLRPC:1.8.1'

    compile project(':vlc')
    compile project(':connectsdk')
}
